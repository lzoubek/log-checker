subprojects {
	apply plugin: 'java'
	apply plugin: 'eclipse'
	apply plugin: 'maven'

	allprojects {
		sourceCompatibility = 1.6
		targetCompatibility = 1.6
	}

	group = 'com.redhat.qe'
	version = '1.0.0'

	eclipse {
		classpath {
			defaultOutputDir = file('bin')
		}
	}

	repositories {
		mavenCentral()
		mavenLocal()
		[
			'https://repository.jboss.org/nexus/content/repositories/thirdparty-uploads',
			'http://clojars.org/repo',
			'http://download.java.net/maven/2/',
			'http://repository.codehaus.org',
			'http://snapshots.repository.codehaus.org'
		].each { repo ->  
			maven {
				url repo
			}
		}
	}
}
project(":log-checker") { 
	dependencies {
	    compile "com.redhat.qe:ssh-tools:1.0.0",
			"org.testng:testng:6.5.1"
    }
}
project(":log-checker-test") {
	dependencies {
		testCompile project(":log-checker"),
    "com.google.inject:guice:3.0"
	}
  task test(dependsOn: "testClasses",overwrite:true) << {
    def xmlSuite = "src/test/java/com/redhat/qe/tools/checklog/test/testng.xml"
    ant.taskdef( resource: 'testngtasks', classpath: project(":log-checker").sourceSets.main.compileClasspath.asPath )
    ant.testng(
      classpath: sourceSets.test.runtimeClasspath.asPath,
      listeners: "com.redhat.qe.tools.checklog.CheckLogTestNGListener",
      outputdir: "$buildDir/reports/tests",
      workingDir: "$buildDir"
    ) {
        xmlfileset(file: "${xmlSuite}")
        // we pass all system properties to testNG except line.sepatator
        // there's an issue that stacktraces are not output on new lines (only god knows why)
        for (def prop in System.properties) {
          if (prop.key.equals("line.separator")) {
            continue
          }
          else {
            sysproperty(key:prop.key,value:prop.value)
            }
        }
    }
  }
}
